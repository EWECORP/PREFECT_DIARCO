USE [data-sync]
GO

/****** Object:  StoredProcedure [dbo].[SP_BASE_PRODUCTOS_SUCURSAL]    Script Date: 28/07/2025 14:49:46 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO


CREATE OR ALTER  PROCEDURE [dbo].[SP_BASE_PRODUCTOS_SUCURSAL]
    @C_SUCU_EMPR INT = NULL,
    @C_FAMILIA INT = NULL,
    @INCLUIR_NO_HABILITADOS BIT = 0
AS
BEGIN
    SET NOCOUNT ON;

    DECLARE @FECHA_FUTURA DATETIME = '2099-12-31';

    -- CTE 1: Artículos en STOCK (surtido base habilitado)
    WITH CTE_Surtido AS (
        SELECT DISTINCT C_ARTICULO
        FROM [DIARCOP001].[DIARCOP].DBO.T060_STOCK
    ),

    -- CTE 2: Habilitaciones en entorno "barrio"
    CTE_MarcaBarrio AS (
        SELECT C_SUCU_EMPR, C_ARTICULO, M_HABILITADO_SUCU
        FROM [diarco-barrio].[diarcobarrio].dbo.T051_ARTICULOS_SUCURSAL
        WHERE C_ARTICULO IN (SELECT C_ARTICULO FROM CTE_Surtido)
    ),

    -- CTE 3: Fechas de vigencia por sucursal-artículo
    CTE_Vigencia AS (
        SELECT
            suc.C_SUCU_EMPR,
            suc.C_ARTICULO,
            ALTA = 
                CASE 
                    WHEN MAX(CASE M_LISTO_PARA_VENTA_ACT WHEN 'S' THEN F_ALTA_SIST END) > MAX(suc.F_ALTA) 
                        THEN MAX(CASE M_LISTO_PARA_VENTA_ACT WHEN 'S' THEN F_ALTA_SIST END)
                    ELSE MAX(suc.F_ALTA)
                END,
            BAJA = ISNULL(
                CASE 
                    WHEN (
                        CASE 
                            WHEN MAX(CASE M_LISTO_PARA_VENTA_ACT WHEN 'S' THEN F_ALTA_SIST END) > MAX(suc.F_ALTA)
                                THEN MAX(CASE M_LISTO_PARA_VENTA_ACT WHEN 'S' THEN F_ALTA_SIST END)
                            ELSE MAX(suc.F_ALTA)
                        END
                    ) > MAX(CASE M_LISTO_PARA_VENTA_ACT WHEN 'N' THEN F_ALTA_SIST END)
                        THEN NULL
                    ELSE MAX(CASE M_LISTO_PARA_VENTA_ACT WHEN 'N' THEN F_ALTA_SIST END)
                END,
                CASE WHEN MAX(M_HABILITADO_SUCU) = 'N' THEN MAX(suc.F_ALTA) ELSE NULL END
            )
        FROM [DIARCOP001].[DIARCOP].dbo.T051_ARTICULOS_SUCURSAL suc
        INNER JOIN [DIARCOP001].[DiarcoP].dbo.t050_articulos art ON art.C_ARTICULO = suc.C_ARTICULO
        LEFT JOIN [DIARCOP001].[DIARCOP].dbo.T804_HIST_MARCA_LISTO_PARA_VENTA hist
            ON hist.C_SUCU_EMPR = suc.C_SUCU_EMPR AND hist.C_ARTICULO = suc.C_ARTICULO
        WHERE suc.C_SUCU_EMPR <> 300 AND art.M_BAJA = 'N'
        GROUP BY suc.C_SUCU_EMPR, suc.C_ARTICULO
    ),

   -- CTE adicional para asegurar proveedor principal
	  CTE_ProveedorPrincipal AS (
		SELECT *
		FROM (
			SELECT 
				C_ARTICULO,
				C_PROVEEDOR,
				Q_FACTOR_PROVEEDOR,
				ROW_NUMBER() OVER (PARTITION BY C_ARTICULO ORDER BY CASE WHEN M_PRINCIPAL = 'S' THEN 0 ELSE 1 END) AS rn
			FROM [DIARCOP001].[DiarcoP].dbo.T052_ARTICULOS_PROVEEDOR
		) x
		WHERE rn = 1
	)

	--- Selección final con JOINs filtrados
		SELECT DISTINCT
			C_SUCU_EMPR = CONVERT(VARCHAR, suc.C_SUCU_EMPR),
			C_ARTICULO = CONVERT(VARCHAR, suc.C_ARTICULO),
			C_PROVEEDOR_PRIMARIO = CONVERT(VARCHAR, art.C_PROVEEDOR_PRIMARIO),
			ABASTECIMIENTO = CONVERT(VARCHAR, suc.C_SISTEMATICA),
			COD_CD = 
			  CASE 
				WHEN suc.C_SISTEMATICA = 0 THEN 
				  CASE 
					WHEN suc.C_SUCU_EMPR < 300 THEN '41CD' 
					WHEN suc.C_SUCU_EMPR IN (49, 60, 68, 71, 76) THEN '82CD'
					ELSE '82CD' 
				  END
				WHEN suc.C_SISTEMATICA = 1 THEN FORMAT(suc.C_SUCU_EMPR, '000')
				WHEN suc.C_SISTEMATICA = 2 THEN 'XDOC'
				WHEN suc.C_SISTEMATICA = 3 THEN 'QX'
				ELSE NULL
			  END,
			HABILITADO = CASE WHEN ((suc.C_SUCU_EMPR < 300 AND suc.M_HABILITADO_SUCU = 'N') OR 
								(suc.C_SUCU_EMPR > 300 AND mb.M_HABILITADO_SUCU = 'N')) THEN '0' ELSE '1' END,
			FECHA_REGISTRO = v.ALTA,
			FECHA_BAJA = ISNULL(v.BAJA, @FECHA_FUTURA),
			UNID_TRANSFERENCIA = '0',
			Q_UNID_TRANSFERENCIA = '1',
			PEDIDO_MIN = ISNULL(CONVERT(VARCHAR, art.Q_BULTOS_KILOS_COMPRA_MINIMA), '0'),
			FRENTE_LINEAL = '1',
			CAPACID_GONDOLA = '1',
			STOCK_MINIMO = '1',
			COD_COMPRADOR = CONVERT(VARCHAR, art.C_COMPRADOR),
			PROMOCION = CASE suc.M_OFERTA_SUCU WHEN 'N' THEN '0' ELSE '1' END,
			ACTIVE_FOR_PURCHASE = CASE WHEN (art.C_FAMILIA = 4 OR art.M_A_DAR_DE_BAJA = 'S' OR 
											 (suc.C_SUCU_EMPR < 300 AND suc.M_HABILITADO_SUCU = 'N') OR 
											 (suc.C_SUCU_EMPR > 300 AND mb.M_HABILITADO_SUCU = 'N')) THEN '0' ELSE '1' END,
			ACTIVE_FOR_SALE = CASE suc.M_LISTO_PARA_VENTA_SUCU WHEN 'N' THEN '0' ELSE '1' END,
			ACTIVE_ON_MIX = CASE WHEN art.C_FAMILIA = 4 OR art.M_A_DAR_DE_BAJA = 'S' THEN '0' ELSE '1' END,
			DELIVERED_ID = CASE suc.C_SISTEMATICA 
							   WHEN 0 THEN CASE WHEN suc.C_SUCU_EMPR < 300 THEN '41CD' ELSE '82' END
							   WHEN 1 THEN CONVERT(VARCHAR, prov.C_PROVEEDOR) 
						   END,
			PRODUCT_BASE_ID = '',
			OWN_PRODUCTION = '0',
			Q_FACTOR_COMPRA = CONVERT(VARCHAR, prov.Q_FACTOR_PROVEEDOR),
			FULL_CAPACITY_PALLET = CONVERT(VARCHAR, art.U_PISO_PALETIZADO * art.U_ALTURA_PALETIZADO),
			NUMBER_OF_LAYERS = CONVERT(VARCHAR, art.U_ALTURA_PALETIZADO),
			NUMBER_OF_BOXES_PER_LAYER = 
				CASE 
					WHEN art.M_VENDE_POR_PESO = 'N' THEN CONVERT(VARCHAR, art.U_PISO_PALETIZADO)
					ELSE CONVERT(VARCHAR, prov.Q_FACTOR_PROVEEDOR) 
				END
		FROM [DIARCOP001].[DIARCOP].dbo.T051_ARTICULOS_SUCURSAL suc
		INNER JOIN [DIARCOP001].[DiarcoP].dbo.t050_articulos art 
				ON art.C_ARTICULO = suc.C_ARTICULO
		LEFT JOIN CTE_MarcaBarrio mb 
				ON mb.C_ARTICULO = suc.C_ARTICULO AND mb.C_SUCU_EMPR = suc.C_SUCU_EMPR
		LEFT JOIN CTE_ProveedorPrincipal prov 
				ON prov.C_ARTICULO = art.C_ARTICULO
		LEFT JOIN [DIARCOP001].[DIARCOP].dbo.T020_Proveedor_Dias_Entrega_Deta entrega 
				ON entrega.C_PROVEEDOR = prov.C_PROVEEDOR AND entrega.C_SUCU_EMPR = suc.C_SUCU_EMPR
		INNER JOIN [DIARCOP001].[DIARCOP].dbo.T100_EMPRESA_SUC SUC_MAE 
				ON SUC_MAE.C_SUCU_EMPR = suc.C_SUCU_EMPR AND SUC_MAE.M_SUCU_VIRTUAL = 'N'
		INNER JOIN CTE_Vigencia v 
				ON v.C_SUCU_EMPR = suc.C_SUCU_EMPR AND v.C_ARTICULO = suc.C_ARTICULO
		WHERE
			suc.C_ARTICULO IN (SELECT C_ARTICULO FROM CTE_Surtido)
			AND suc.C_SUCU_EMPR NOT IN (SELECT C_SUCU_EMPR FROM [data-sync].[dbo].[SUCURSALES_EXCLUIDAS])
			AND (@C_SUCU_EMPR IS NULL OR suc.C_SUCU_EMPR = @C_SUCU_EMPR)
			AND (@C_FAMILIA IS NULL OR art.C_FAMILIA = @C_FAMILIA)
			AND (
				@INCLUIR_NO_HABILITADOS = 1 OR 
				((art.C_FAMILIA <> 4) AND (art.M_A_DAR_DE_BAJA <> 'S') AND suc.M_HABILITADO_SUCU = 'S')
			);

END
GO


